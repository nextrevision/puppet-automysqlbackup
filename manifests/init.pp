# == Class: automysqlbackup
#
# Puppet module to install AutoMySQLBackup for periodic MySQL backups.
#
# === Variables
#
# [bin_dir]
#   - Location for the automysqlbackup binary file
#
# [etc_dir]
#   - Location to store all configurations for AMB
#
# [backup_dir]
#   - Location to store backups generated by AMB
#
# [source]
#   - The source of the automysqlbackup script
#
# [install_multicore]
#   - Boolean to install multicore compression support (assumes packages
#     available in repo). If RedHat family, these packages can be found in the
#     RPMforge repos
#
# [config]
#   - Takes a hash passed by Hiera to create backup resources
#
# [config_defaults]
#   - Takes a hash of defaults passed by Hiera to create resources
#
# === Examples
#
#  # Assume the defaults
#  include automysqlbackup
#
#  # With a custom backup directory
#  class { 'automysqlbackup':
#    backup_dir  => /mnt/backups,
#  }
#
# === Authors
#
# NextRevision <notarobot@nextrevision.net>
#
# === Copyright
#
# Copyright 2013 NextRevision, unless otherwise noted.

class automysqlbackup (
  Stdlib::Absolutepath $bin_dir           = '/usr/local/bin',
  Stdlib::Absolutepath $etc_dir           = '/etc/automysqlbackup',
  Stdlib::Absolutepath $backup_dir        = '/var/backup',
  Stdlib::Filesource   $source            = 'puppet:///modules/automysqlbackup/automysqlbackup',
  Boolean              $install_multicore = false,
  Hash                 $config            = {},
  Hash                 $config_defaults   = {},
) {
  # Create a subdirectory in /etc for config files
  file { $etc_dir:
    ensure => directory,
    owner  => 'root',
    group  => 'root',
    mode   => '0750',
  }

  # Create an example backup file, useful for reference
  file { "${etc_dir}/automysqlbackup.conf.example":
    ensure => file,
    owner  => 'root',
    group  => 'root',
    mode   => '0660',
    source => 'puppet:///modules/automysqlbackup/automysqlbackup.conf',
  }

  # Add files from the developer
  file { "${etc_dir}/AMB_README":
    ensure => file,
    source => 'puppet:///modules/automysqlbackup/AMB_README',
  }
  file { "${etc_dir}/AMB_LICENSE":
    ensure => file,
    source => 'puppet:///modules/automysqlbackup/AMB_LICENSE',
  }

  # Install the actual binary file
  file { "${bin_dir}/automysqlbackup":
    ensure => file,
    owner  => 'root',
    group  => 'root',
    mode   => '0755',
    source => $source,
  }

  # Create the base backup directory
  file { $backup_dir:
    ensure => directory,
    owner  => 'root',
    group  => 'root',
    mode   => '0755',
  }

  # If you'd like to keep your config in hiera and pass it to this class
  $_config = deep_merge($config_defaults,$config)
  $_config.each |$key,$value| {
    automysqlbackup::backup { $key:
      * => $value,
    }
  }

  # If using RedHat family, must have the RPMforge repo's enabled
  if $install_multicore {
    package { ['pigz', 'pbzip2']:
      ensure => installed,
    }
  }

}
